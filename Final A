// PWM input pins, any of the following pins can be used: digital 0 - 13 or analog A0 - A5

const int pwmPIN[] = {6, 10, 12, 5, 7, 8}; // an array to identify the PWM input pins (the array can be any length)
const int RudderOutpinPin = 3;  // identify the pins that each servo signal wire is connected to
const int LeftMotorPin = 11;
const int RightMotorPin = 9;

// first pin is channel 1, second is channel 2...etc
int RC_inputs = 6;                // The number of pins in pwmPIN that are connected to an RC receiver. Addition pins not connected to an RC receiver could be used for any other purpose i.e. detecting the echo pulse on an HC-SR04 ultrasonic distance sensor
// When 0, it will automatically update to the number of pins specified in pwmPIN[] after calling setup_pwmRead().
// Calibration of each RC channel:

// The arrays below are used to calibrate each RC channel into the range -1 to +1 so that servo direction, mixing, rates, sub trims...etc can be applied in sketch depending on application.
// The arrays should be modified in order to calibrate the min, middle and max pulse durations to suit your transmitter (use max rates to get the best resolution).
// FYI: the function print_PWM() will print the raw pulse width data for all the RC channels to the serial port.
// if the RC_min[], RC_mid[], RC_max[] are empty or have missing data the calibration will default to min 1000us, mid 1500us and max 2000us.

//SANWA 6CH 40MHz with corona RP6D1
//                RUD - 6    THR - 10    HOLD - 12    ROT - 5     DIAL -7   SLIDER -8
int RC_min[6] = { 992,       780,        984,         984,        1056,   1116};
int RC_mid[6] = { 1520,      1538,       1538,        1538,       1374,   1460};
int RC_max[6] = { 2004,      2098,       1992,        1992,       1876,   1796};


// fail safe positions

float RC_failsafe[] = {1527, 1549, 2000, 2001, 2000, 0.00};

// enter a failsafe position (in the range of -+1) for each RC channel in case radio signal is lost
// if the array is the incorrect length for the number of RC channels, the failsafe will default to neutral i.e. 0.
// The failsafe tolerances are: 10-330Hz & 500-2500us

/*
      GLOBAL PWM DECODE VARIABLES
*/

const int num_ch = sizeof(pwmPIN) / sizeof(int); // calculate the number of input pins (or channels)
volatile int PW[num_ch];                        // an array to store pulsewidth measurements
volatile boolean prev_pinState[num_ch];         // an array used to determine whether a pin has gone low-high or high-low
volatile unsigned long pciTime;                 // the time of the current pin change interrupt
volatile unsigned long pwmTimer[num_ch];        // an array to store the start time of each PWM pulse

volatile boolean pwmFlag[num_ch];               // flag whenever new data is available on each pin
volatile boolean RC_data_rdy;                   // flag when all RC receiver channels have received a new pulse
unsigned long pwmPeriod[num_ch];                 // period, mirco sec, between two pulses on each pin

byte pwmPIN_reg[num_ch];                        // each of the input pins expressed as a position on it's associated port register
byte pwmPIN_port[num_ch];                       // identify which port each input pin belongs to (0 = PORTB, 1 = PORTC, 2 = PORTD)

const int size_RC_min = sizeof(RC_min) / sizeof(int);           // measure the size of the calibration and failsafe arrays
const int size_RC_mid = sizeof(RC_mid) / sizeof(int);
const int size_RC_max = sizeof(RC_max) / sizeof(int);
const int size_RC_failsafe = sizeof(RC_failsafe) / sizeof(float);

// FUNCTION USED TO TURN ON THE INTERRUPTS ON THE RELEVANT PINS
// code from http://playground.arduino.cc/Main/PinChangeInterrupt

void pciSetup(byte pin) {
  *digitalPinToPCMSK(pin) |= bit (digitalPinToPCMSKbit(pin));  // enable pin
  PCIFR  |= bit (digitalPinToPCICRbit(pin));                   // clear any outstanding interrupt
  PCICR  |= bit (digitalPinToPCICRbit(pin));                   // enable interrupt for the group
}

// FUNCTION USED TO FIND THE PIN POSITION ON EACH PORT REGISTER: helps the interrupt service routines, ISR, run faster

void pwmPIN_to_port() {
  for (int i = 0; i < num_ch; i++) {

    // determine which port and therefore ISR (PCINT0_vect, PCINT1_vect or PCINT2_vect) each pwmPIN belongs to.
    pwmPIN_port[i] = 1;    // pin belongs to PCINT1_vect (PORT C)
    if (pwmPIN[i] >= 0 && pwmPIN[i] <= 7)                         pwmPIN_port[i] = 2;    // pin belongs to PCINT2_vect (PORT D)
    else if (pwmPIN[i] >= 8 && pwmPIN[i] <= 13)                   pwmPIN_port[i] = 0;    // pin belongs to PCINT0_vect (PORT B)

    // covert the pin number (i.e. pin 11 or pin A0) to the pin position in the port register. There is most likely a better way of doing this using a macro...
    // (Reading the pin state directly from the port registers speeds up the code in the ISR)

    if (pwmPIN[i] == 0 || pwmPIN[i] == A0 || pwmPIN[i] == 8)         pwmPIN_reg[i] = 0b00000001;
    else if (pwmPIN[i] == 1 || pwmPIN[i] == A1 || pwmPIN[i] == 9)    pwmPIN_reg[i] = 0b00000010;
    else if (pwmPIN[i] == 2 || pwmPIN[i] == A2 || pwmPIN[i] == 10)   pwmPIN_reg[i] = 0b00000100;
    else if (pwmPIN[i] == 3 || pwmPIN[i] == A3 || pwmPIN[i] == 11)   pwmPIN_reg[i] = 0b00001000;
    else if (pwmPIN[i] == 4 || pwmPIN[i] == A4 || pwmPIN[i] == 12)   pwmPIN_reg[i] = 0b00010000;
    else if (pwmPIN[i] == 5 || pwmPIN[i] == A5 || pwmPIN[i] == 13)   pwmPIN_reg[i] = 0b00100000;
    else if (pwmPIN[i] == 6)                                         pwmPIN_reg[i] = 0b01000000;
    else if (pwmPIN[i] == 7)                                         pwmPIN_reg[i] = 0b10000000;

  }
}

// SETUP OF PIN CHANGE INTERRUPTS

void setup_pwmRead() {

  for (int i = 0; i < num_ch; i++) {            // run through each input pin
    pciSetup(pwmPIN[i]);                        // enable pinchange interrupt for pin
  }
  pwmPIN_to_port();                             // determines the port for each input pin
  // pwmPIN_to_port() also coverts the pin number in pwmPIN[] (i.e. pin 11 or pin A0) to the pin position in the port register (i.e. 0b00000001) for use in the ISR.

  if (RC_inputs == 0 || RC_inputs > num_ch) RC_inputs = num_ch;   // define the number of pins connected to an RC receiver.
}

// INTERRUPT SERVICE ROUTINES (ISR) USED TO READ PWM INPUT

// the PCINT0_vect (B port register) reacts to any changes on pins D8-13.
// the PCINT1_vect (C port register)          ""        ""         A0-A5.
// the PCINT2_vect (D port register)          ""        ""         D0-7.

// port registers are used to speed up if statements in ISR code:
// https://www.arduino.cc/en/Reference/PortManipulation http://tronixstuff.com/2011/10/22/tutorial-arduino-port-manipulation/
// http://harperjiangnew.blogspot.co.uk/2013/05/arduino-port-manipulation-on-mega-2560.html


// READ INTERRUPTS ON PINS D8-D13: ISR routine detects which pin has changed, and returns PWM pulse width, and pulse repetition period.

ISR(PCINT0_vect) {                                                // this function will run if a pin change is detected on portB

  pciTime = micros();                                             // Record the time of the PIN change in microseconds

  for (int i = 0; i < num_ch; i++) {                              // run through each of the channels
    if (pwmPIN_port[i] == 0) {                                    // if the current channel belongs to portB

      if (prev_pinState[i] == 0 && PINB & pwmPIN_reg[i]) {        // and the pin state has changed from LOW to HIGH (start of pulse)
        prev_pinState[i] = 1;                                     // record pin state
        pwmPeriod[i] = pciTime - pwmTimer[i];                     // calculate the time period, micro sec, between the current and previous pulse
        pwmTimer[i] = pciTime;                                    // record the start time of the current pulse
      }
      else if (prev_pinState[i] == 1 && !(PINB & pwmPIN_reg[i])) { // or the pin state has changed from HIGH to LOW (end of pulse)
        prev_pinState[i] = 0;                                     // record pin state
        PW[i] = pciTime - pwmTimer[i];                            // calculate the duration of the current pulse
        pwmFlag[i] = HIGH;                                        // flag that new data is available
        if (i + 1 == RC_inputs) RC_data_rdy = HIGH;
      }
    }
  }
}

// READ INTERRUPTS ON PINS A0-A5: ISR routine detects which pin has changed, and returns PWM pulse width, and pulse repetition period.

ISR(PCINT1_vect) {                                                // this function will run if a pin change is detected on portC

  pciTime = micros();                                             // Record the time of the PIN change in microseconds

  for (int i = 0; i < num_ch; i++) {                              // run through each of the channels
    if (pwmPIN_port[i] == 1) {                                    // if the current channel belongs to portC

      if (prev_pinState[i] == 0 && PINC & pwmPIN_reg[i]) {        // and the pin state has changed from LOW to HIGH (start of pulse)
        prev_pinState[i] = 1;                                     // record pin state
        pwmPeriod[i] = pciTime - pwmTimer[i];                     // calculate the time period, micro sec, between the current and previous pulse
        pwmTimer[i] = pciTime;                                    // record the start time of the current pulse
      }
      else if (prev_pinState[i] == 1 && !(PINC & pwmPIN_reg[i])) { // or the pin state has changed from HIGH to LOW (end of pulse)
        prev_pinState[i] = 0;                                     // record pin state
        PW[i] = pciTime - pwmTimer[i];                             // calculate the duration of the current pulse
        pwmFlag[i] = HIGH;                                         // flag that new data is available
        if (i + 1 == RC_inputs) RC_data_rdy = HIGH;
      }
    }
  }
}

// READ INTERRUPTS ON PINS D0-7: ISR routine detects which pin has changed, and returns PWM pulse width, and pulse repetition period.

ISR(PCINT2_vect) {                                                // this function will run if a pin change is detected on portD

  pciTime = micros();                                             // Record the time of the PIN change in microseconds

  for (int i = 0; i < num_ch; i++) {                              // run through each of the channels
    if (pwmPIN_port[i] == 2) {                                    // if the current channel belongs to portD

      if (prev_pinState[i] == 0 && PIND & pwmPIN_reg[i]) {        // and the pin state has changed from LOW to HIGH (start of pulse)
        prev_pinState[i] = 1;                                     // record pin state
        pwmPeriod[i] = pciTime - pwmTimer[i];                     // calculate the time period, micro sec, between the current and previous pulse
        pwmTimer[i] = pciTime;                                    // record the start time of the current pulse
      }
      else if (prev_pinState[i] == 1 && !(PIND & pwmPIN_reg[i])) { // or the pin state has changed from HIGH to LOW (end of pulse)
        prev_pinState[i] = 0;                                     // record pin state
        PW[i] = pciTime - pwmTimer[i];                            // calculate the duration of the current pulse
        pwmFlag[i] = HIGH;                                        // flag that new data is available
        if (i + 1 == RC_inputs) RC_data_rdy = HIGH;
      }
    }
  }
}

/*
    RC OUTPUT FUNCTIONS
*/

boolean RC_avail() {
  boolean avail = RC_data_rdy;
  RC_data_rdy = LOW;                          // reset the flag
  return avail;
}

float RC_decode(int CH) {

  if (CH < 1 || CH > RC_inputs) return 0;    // if channel number is out of bounds return zero.

  int i = CH - 1;

  // determine the pulse width calibration for the RC channel. The default is 1000, 1500 and 2000us.

  int Min;
  if (CH <= size_RC_min) Min = RC_min[CH - 1]; else Min = 1000;

  int Mid;
  if (CH <= size_RC_mid) Mid = RC_mid[CH - 1]; else Mid = 1500;

  int Max;
  if (CH <= size_RC_max) Max = RC_max[CH - 1]; else Max = 2000;

  float CH_output;

  if (FAILSAFE(CH) == HIGH) {                       // If the RC channel is outside of failsafe tolerances (10-330hz and 500-2500uS)
    if (CH > size_RC_failsafe) CH_output = 0;     // and if no failsafe position has been defined, set output to neutral
    else CH_output = RC_failsafe[i];              // or if defined set the failsafe position
  }
  else {                                            // If the RC signal is valid
    CH_output = calibrate(PW[i], Min, Mid, Max);    // calibrate the pulse width to the range -1 to 1.
  }
  return CH_output;

  // The signal is mapped from a pulsewidth into the range of -1 to +1, using the user defined calibrate() function in this code.

  // 0 represents neutral or center stick on the transmitter
  // 1 is full displacement of a control input is one direction (i.e full left rudder)
  // -1 is full displacement of the control input in the other direction (i.e. full right rudder)
}

/*
    Receiver Calibration
*/

// NEED TO SPEED UP

float calibrate(float Rx, int Min, int Mid, int Max) {
  float calibrated;
  if (Rx >= Mid)
  {
    calibrated = map(Rx, Mid, Max, 0, 1000);  // map from 0% to 100% in one direction
  }
  else if (Rx == 0)
  {
    calibrated = 0;                           // neutral
  }
  else
  {
    calibrated = map(Rx, Min, Mid, -1000, 0); // map from 0% to -100% in the other direction
  }
  return calibrated * 0.001;
}

// Basic Receiver FAIL SAFE
// check for 500-2500us and 10-330Hz (same limits as pololu)

boolean FAILSAFE(int CH) {

  int i = CH - 1;
  boolean failsafe_flag = LOW;

  if (pwmFlag[i] == 1)                            // if a new pulse has been measured.
  {
    pwmFlag[i] = 0;                            // set flag to zero

    if (pwmPeriod[i] > 100000)                 // if time between pulses indicates a pulse rate of less than 10Hz
    {
      failsafe_flag = HIGH;
    }
    else if (pwmPeriod[i] < 3000)              // or if time between pulses indicates a pulse rate greater than 330Hz
    {
      failsafe_flag = HIGH;
    }

    if (PW[i] < 500 || PW[i] > 2500)          // if pulswidth is outside of the range 500-2500ms
    {
      failsafe_flag = HIGH;
    }
  }
  else if (micros() - pwmTimer[i] > 100000)     // if there is no new pulswidth measurement within 100ms (10hz)
  {
    failsafe_flag = HIGH;
  }

  return failsafe_flag;
}

/*
    Quick print function of Rx channel input
*/

void print_RCpwm() {                            // display the raw RC Channel PWM Inputs
  for (int i = 0; i < RC_inputs; i++) {
    //Serial.print(" ch");Serial.print(i+1);
    //Serial.print("  ");
    if (PW[i] < 1000) Serial.print(" ");
    Serial.print(PW[i]);
  }
  Serial.println("");
}

void print_decimal2percentage(float dec) {
  int pc = dec * 100;
  // the number and text will take up 6 charactors i.e ___3%_ or -100%_
  if (pc >= 0) Serial.print(" ");
  if (abs(pc) < 100) Serial.print(" ");
  if (abs(pc) < 10) Serial.print(" ");
  Serial.print(" "); Serial.print(pc); Serial.print("% ");
}

/*
   GENERIC PWM FUNCTIONS
*/

unsigned long pin_time;
float pin_pwm;
float pin_period;

boolean PWM_read(int CH) {
  if (CH < 1 && CH > num_ch) return false;
  int i = CH - 1;
  boolean avail = pwmFlag[i];
  if (avail == HIGH) {
    pwmFlag[i] = LOW;
    noInterrupts();
    pin_time = pwmTimer[i];
    pin_pwm = PW[i];
    pin_period = pwmPeriod[i];
    interrupts();
  }
  return avail;
}

unsigned long PWM_time() {
  return pin_time;
}
float PWM_period() {
  return pin_period;
}
float PWM() {
  return pin_pwm;
}
float PWM_freq() {
  float freq;
  return freq = 1000000 / pin_period;  // frequency Hz
}
float PWM_duty() {
  float duty;
  duty = pin_pwm / pin_period;
  return duty;
}

unsigned long now;                       // timing variables to update data at a regular interval
unsigned long rc_update;
const int channels = 6;                   // specify the number of receiver channels
float RC_in[channels];                    // an array to store the calibrated input from receiver





// Each servo must be attached to a pin that has a PWM output
// on the arduino uno, nano and pro mini these pins are 3, 5, 6, 9, 10 and 11

// Select Servo Direction, Rates and Sub-trim (the size of each array must match the number of servos)

boolean servo_dir[] = {0, 0, 0, 0};  // Direction: 0 is normal, 1 is reverse
float servo_rates[] = {1, 1, 1, 1}; // Rates: range 0 to 2 (1 = +-500us (NORMAL), 2 = +-1000us (MAX)): The amount of servo deflection in both directions
float servo_subtrim[] = {0, 0, 0, 0}; // Subtrimrange -1 to +1 (-1 = 1000us, 0 = 1500us, 1 = 2000us): The neutral position of the servo
boolean servo_mix_on = false;


int calc_uS(float cmd, int servo) {                               // cmd = commanded position +-100%
  // servo = servo num (to apply correct direction, rates and trim)
  int i = servo - 1;
  float dir;

  if (servo_dir[i] == 0) dir = -1; else dir = 1;                  // set the direction of servo travel

  cmd = 1500 + (cmd * servo_rates[i] * dir + servo_subtrim[i]) * 500; // apply servo rates and sub trim, then convert to a uS value

  if (cmd > 2500) cmd = 2500;                                     // limit pulsewidth to the range 500 to 2500us
  else if (cmd < 500)
  {
    cmd = RC_failsafe[i];
  }

  return cmd;
}

#include <TinyGPS.h>
#include <LiquidCrystal.h>
#include <Servo.h> // include the servo library to control the servos
#include <SPI.h>
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>



#define SCREEN_WIDTH 128 // OLED display width, in pixels
#define SCREEN_HEIGHT 32 // OLED display height, in pixels

#define OLED_RESET     4 // Reset pin # (or -1 if sharing Arduino reset pin)
#define SCREEN_ADDRESS 0x3C ///< See datasheet for Address; 0x3D for 128x64, 0x3C for 128x32
Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, OLED_RESET);

Servo Rudder;
Servo RightMotor;
Servo LeftMotor;
TinyGPS gps;

int RudderValue;      // variables to store the pulse widths to be sent to the servo
int ThrottleValue;
int rotateValue;
int holdValue;

bool holdOn = false;
bool rotateOn = false;


bool newData = false;
unsigned long chars;
unsigned short sentences, failed;
float flat = 42.3057, flon = -71.1162;
int LCDINTERVAL = 1000;
unsigned long time_now = 0;
float maxSpeed = 0.0;



void bootSequence() {

  display.clearDisplay();

  display.setTextSize(2); // Draw 2X-scale text
  display.setTextColor(SSD1306_WHITE);
  display.setCursor(10, 0);
  display.println(F("Booting..."));
  display.display();
  display.dim(false);
  delay(1000);
}

void setupI2C() {

  if (!display.begin(SSD1306_SWITCHCAPVCC, SCREEN_ADDRESS)) {
    Serial.println(F("SSD1306 allocation failed"));
    for (;;); // Don't proceed, loop forever
  }

  display.display();
  display.clearDisplay();
}

void setupRC() {

  Rudder.attach(RudderOutpinPin);
  RightMotor.attach(RightMotorPin);
  LeftMotor.attach(LeftMotorPin);
}

void setup() {

  //Setup Display
  setupI2C();

  //Show boot message..
  bootSequence();

  //Setup motor / rudder out
  setupRC();

  //setup RC read
  setup_pwmRead();


  Serial.begin(9600);

  // Random stuff
  pinMode(LED_BUILTIN, OUTPUT);
  pinMode(3, OUTPUT);

}

void loop() {

  now = millis();

  if (RC_avail() || now - rc_update > 25) { // if RC data is available or 25ms has passed since last update (adjust to > frame rate of receiver)

    rc_update = now;

    for (int i = 0; i < channels; i++) {    // run through each RC channel
      int CH = i + 1;
      RC_in[i] = RC_decode(CH);             // decode receiver channel and apply failsafe
    }
  }

  //Read RC Values
  RudderValue = calc_uS(RC_in[0], 1);
  ThrottleValue = calc_uS(RC_in[1], 2);
  holdValue = calc_uS(RC_in[2], 3);
  rotateValue = calc_uS(RC_in[3], 4);

  //Process Status
  if (holdValue > 1500) {
    holdOn = false;
  } else {
    holdOn = true;
  }

  if (rotateValue > 1500) {
    rotateOn = false;
  } else {
    rotateOn = true;
  }

  if (holdOn) {
    // GPS Code will go here
    //lets use this for slow cruise
    RightMotor.writeMicroseconds(1565);
    LeftMotor.writeMicroseconds(1565);
    Rudder.writeMicroseconds(RudderValue);
  }
  else if (rotateOn)
  {
    RightMotor.writeMicroseconds(1484);
    LeftMotor.writeMicroseconds(1565);
  }
  else
  {
    Rudder.writeMicroseconds(RudderValue);
    RightMotor.writeMicroseconds(ThrottleValue);
    LeftMotor.writeMicroseconds(ThrottleValue);
  }

  // GPS Parsing
  while (Serial.available())
  {
    char c = Serial.read();
    if (gps.encode(c)) // Did a new valid sentence come in?
      newData = true;
  }

  if (millis() > time_now + LCDINTERVAL) {
    time_now = millis();

    writeLCD();

    if (newData)
    {
      unsigned long age;
      gps.f_get_position(&flat, &flon, &age);
    }

    gps.stats(&chars, &sentences, &failed);

    if ( gps.f_speed_kmph() > maxSpeed)
    {
      maxSpeed = gps.f_speed_kmph();
    }

    time_now = now;
  }

}

void writeLCD(){

  display.clearDisplay();

  display.setTextSize(1);             // Normal 1:1 pixel scale
  display.setTextColor(WHITE);        // Draw white text
  display.setCursor(0, 0);            // Start at top-left corner

  display.println("SATS:" + String(gps.satellites()) + " | C: " + String(gps.f_course()));
  display.println("M:" + String(maxSpeed) + " |H:" + String(holdOn ? " * " : "0") + " |R:" + String((rotateOn ? "*" : "0")));
  
  if(gps.satellites() > 100)
  {
    display.println("*-- NO LOCK --*");  
  }
  else
  {
    display.println(String(flat) + "," +  String(flon));  
  }  
  
  display.println("T:" + String(ThrottleValue) + " | R: " + String(RudderValue) );
  display.display();

}
